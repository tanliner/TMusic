// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'versions.gradle'
    addRepos(repositories)
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.kotlin.plugin
        classpath deps.google_services
        classpath deps.andromeda_plugin
        classpath deps.butterknife_gradle_plugin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    addRepos(repositories)
}

subprojects {
    if (name == "app" || name == "token") {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    // kotlin support for every module
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
    // kotlin support end

    android {
        compileSdkVersion build_versions.compile_sdk
        buildToolsVersion build_versions.build_tools

        // for module name rule
        if (name != "style" && name != "widget") {
            // resourcePrefix project.getName() + "_"
        }
        defaultConfig {
            minSdkVersion build_versions.min_sdk
            targetSdkVersion build_versions.target_sdk
            versionCode build_versions.version_code
            versionName build_versions.version_name
        }

        // Parcelable
        androidExtensions {
            experimental = true
        }

        compileOptions {
            sourceCompatibility build_versions.source_compatibility
            targetCompatibility build_versions.target_compatibility
        }

        // for android test, like Base64
        testOptions {
            unitTests.returnDefaultValues = true
        }

        buildTypes {
            debug {
                minifyEnabled false
            }

            release {
                minifyEnabled true
                consumerProguardFiles 'proguard-rules.pro'
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
